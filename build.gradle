plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
        id 'com.github.node-gradle.node' version '7.0.1'
}

group = 'zero'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
        runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        implementation group: "net.sf.oval", name: "oval", version: "3.2.1"

        // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox
        implementation group: 'org.apache.pdfbox', name: 'pdfbox', version: '3.0.4'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named("processResources") {
    dependsOn copyFrontendBuild
}

def reactAppDir = "$projectDir/frontend"

task installFrontend(type: NpmTask) {
    workingDir = file(reactAppDir)
    args = ['install']
}

task buildFrontend(type: NpmTask) {
    dependsOn installFrontend
    workingDir = file(reactAppDir)
    args = ['run', 'build']
}

task copyFrontendBuild(type: Copy) {
    dependsOn buildFrontend
    from "$reactAppDir/build"
    into "$projectDir/src/main/resources/static"
}

bootJar {
    dependsOn copyFrontendBuild
}

node {
    version = '23.11.0' // Node.js version
    npmVersion = '11.2.0' // npm version
    download = true // Auto-download Node.js
}
